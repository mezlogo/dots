gradle.projectsEvaluated { Gradle gradle ->
  gradle.rootProject.allprojects { Project project ->
    if (project.pluginManager.hasPlugin('java')) {
      project.task([type: JavaExec], 'r', {
        classpath = project.sourceSets.main.runtimeClasspath
        main = project.hasProperty('rM') ? project['rM'] : "Main"
        if (project.hasProperty('rA')) args project['rA'].split(' ')
        if (project.hasProperty('rV')) jvmArgs project['rV'].split(' ')
        if (project.hasProperty('rD')) {
          //valid input: '', 'y', '5005', '5005y'
          def debugProp = project['rD']
          def suspend = debugProp.endsWith('y') ? 'y' : 'n'
          def potentialPort = debugProp.replaceAll('y', '')
          def debugPort = potentialPort.isNumber() ? potentialPort : 5005
          jvmArgs "-agentlib:jdwp=transport=dt_socket,server=y,suspend=$suspend,address=$debugPort"
        }
        if (project.hasProperty('rT')) {
          jvmArgs '-javaagent:' + System.getProperty('user.home') + '/tools/slf4jextJ.jar'
        }
      })
    }
  }
}

